//~~~~~~~User's Birthday
//I moved OnGetBirthday and associated functions from menu.dic to a seperate .dic to make it more flexible.
//At least I think it's better. It's certainly more appealing to me to do it this way. <3Era

OnGetBirthday	
{
	//This is setting up in case today is the user's birthday.
	dayslot = GetDaySlot 
	"\0\s[0]\_v[trill.wav]What month were you born? \w8Write the number.\![open,inputbox,OnUserBornMonth,-1]\e"
}
	
//----OnUserBornMonth----
//This takes the value that the user put into the input box above and evaluates it.

OnUserBornMonth
{
	//this stores what the user input in as a variable called _tempmonth
	_tempmonth = reference0 
	//if the month is a valid month
	if _tempmonth >= 1 && _tempmonth <= 12 
	{
		bornmonth = _tempmonth
		bornmonthprint = Printmonth
			
		"\0\s[0]\_v[trill.wav]So you were born in %(bornmonthprint).\n\w8What day was it?\![open,inputbox,OnUserBornDay,-1]\e"
				
	}
	//if the month isn't a valid month, like 32 or something.
	else 
	{
		"\0\s[0]\_v[trill.wav]Very funny, but I only know of 12 months.\nTry again.\![open,inputbox,OnUserBornMonth,-1]\e"
			
	}
}

//This puts the name to the number of the month. No need to mess with these.
Printmonth
{
	if bornmonth == 1
	{
		"January"
	}
	elseif bornmonth == 2
	{
		"February"
	}
	elseif bornmonth == 3
	{
		"March"
	}
	elseif bornmonth == 4
	{
		"April"
	}
	elseif bornmonth == 5
	{
		"May"
	}
	elseif bornmonth == 6
	{
		"June"
	}
	elseif bornmonth == 7
	{
		"July"
	}
	elseif bornmonth == 8
	{
		"August"
	}
	elseif bornmonth == 9
	{
		"September"
	}
	elseif bornmonth == 10
	{
		"October"
	}
	elseif bornmonth == 11
	{
		"November"
	}
	else
	{
		"December"
	}
}

//This adds an appropriate suffix to the number for the day. No need to mess with this either.

Printday
{
	if bornday == 1 || bornday == 21 || bornday == 31
	{
	"%(bornday)st"
	}
	elseif bornday == 2 || bornday == 22
	{
	"%(bornday)nd"
	}
	elseif bornday == 3 || bornday == 23
	{
	"%(bornday)rd"
	}
	else
	{
	"%(bornday)th"
	}
}	

//----OnUserBornDay----
//This is much like the above function OnUserBornMonth. It'll take the value the user put in for the day and analyze it.

OnUserBornDay
{
	//storing the input in tempday
	tempday = reference0 
	
	//This is checking to see if it's one of the months with 31 days.There's probably a more efficient way to do this but I don't know it.
	if bornmonth == 1 || bornmonth == 3 || bornmonth == 5 || bornmonth == 7 || bornmonth == 8 || bornmonth == 10 || bornmonth == 12 
	{
		//if it falls in the right range
		if tempday >= 1 && tempday <= 31 
		{
			bornday = tempday
			borndayprint = Printday
			userbirthday = "%(bornmonth)month %(bornday)day"
			birthdayprint = "%(bornmonthprint) %(borndayprint)"
					
			"%(birthchecked)"
		}
		//if the day isn't valid
		else 
		{
			"\0\s[0]\_v[trill.wav]%(bornmonthprint) only has 31 days... Try again.\![open,inputbox,OnUserBornDay,-1]\e"
		}
	}
	elseif bornmonth == 2 //if the month is february
	{
		if tempday >= 1 && tempday <= 29 
		{
			bornday = tempday
			borndayprint = Printday
			userbirthday = "%(bornmonth)month %(bornday)day"
			birthdayprint = "%(bornmonthprint) %(borndayprint)"
					
			"%(birthchecked)"
		}
		else
		{
			"\0\s[0]\_v[trill.wav]%(bornmonthprint) only has 29 days at most, try again.\![open,inputbox,OnUserBornDay,-1]\e"
				}
		}
		//if it's any other month than those
	else 
	{
		if tempday >= 1 && tempday <= 30 //if the day falls between these values
		{
			bornday = tempday
			borndayprint = Printday
			userbirthday = "%(bornmonth)month %(bornday)day"
			birthdayprint = "%(bornmonthprint) %(borndayprint)"
					
			"%(birthchecked)"
		}
		else
		{
			"\0\s[0]\_v[trill.wav]%(bornmonthprint) has 30 days. Try again.\![open,inputbox,OnUserBornDay,-1]\e"
		}
	}		
}

//----birthchecked----


birthchecked
{
	//if today is the user's birthday
	if userbirthday == dayslot 
	{
		"\0\s[0]\_v[trill.wav]Happy birthday, %(username).\e"
		
	}
	//Any number of these can be added to check birthdays against holidays
	elseif userbirthday == "12month 25day"
	{
		"\0\s[0]\_v[trill.wav]A Christmas child, huh? Is that a good or bad thing...?\e"
	}
	//if the birthday is not listed above.
	else 
	{
		"\0\s[0]\_v[trill.wav]Your birthday is %(bornmonthprint) %(borndayprint). I'll remember.\e"
			
	}
}