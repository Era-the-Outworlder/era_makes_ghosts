//---------------------------The User's Name--------------------------
//This .dic file has to do with setting the user's name and suffix/prefix. (I added some easter eggs in the name check, as well as adding Mx. to the titles <3 Era)


//--OnTeachName
//This is called from the config menu in the main character's double click menu when the user says they want to change their name. 

OnTeachName	
{
	if reference0 == "TEACHNAME" //ignore this
		{
		"\0\s[0]\_v[HELP_text.mp3] * (Change var_player_name?) \![open,inputbox,OnNameTeach,-1]\s[0]\e"
		"\0\s[12]\_v[sound_FLOWEY.mp3] * Name the Fallen Human \![open,inputbox,OnNameTeach,-1]\s[0]\e"
		"\0\s[16]\_v[sound_FLOWEY.mp3] * Human name name name fallen human fallen fallen... name? \![open,inputbox,OnNameTeach,-1]\s[0]\e"
		
		}
	else
		{

		}
}


//--OnNameTeach
//This is where the user inputs their name and the ghost analyzes it. 

OnNameTeach
{
	tempname = reference0 //this stores the name value temporarily in reference0
	
	if reference0 == "flowey" || reference0 == "Flowey" || reference0 == "FLOWEY" //this checks to see if the user input the name flowey.
	{
		"\0\s[9]\_v[sound_FLOWEY.mp3] * Chose... already...\![open,inputbox,OnNameTeach,-1]"
		
	}
	elseif reference0 == "FlOwEy" || reference0 == "fLoWeY"
	{
		"\0\s[5]\_v[sound_FLOWEY.mp3] * Clevvvvvvvver Vvve ver very clever.\![open,inputbox,OnNameTeach,-1]"
		"\0\s[8]\_v[HELP_text.mp3] * (You decide that may have been a bad idea.)\![open,inputbox,OnNameTeach,-1]"
		
	}
	elseif reference0 == "" || reference0 == " " || reference0 == "   " //This is if the user doesn't put in a name at all.
	{
		username = "NULL" //this sets the user's name.
		
		"\0\s[14]\_v[sound_FLOWEY.mp3] * var_player_name = %(username)\s[0]\e"
		"\0\s[14]\_v[HELP_text.mp3] * var_player_name = %(username) Is this correct? \n/
		\![*]\q[Change it.,TEACHNAME]\n/
		\![*]\q[It's fine.,CANCEL]\n\s[0]\e"
		
	}
	elseif reference0 == "sans" || reference0 == "Sans" || reference0 == "SANS" //This is if the user calls themself Sans
	{
		username = "Smiley Trashbag" //this sets the user's name to something insulting
		
		"\0\s[8]\_v[sound_FLOWEY.mp3] * Trash_bag_ \s[0]\e"
		"\0\s[8]\_v[HELP_text.mp3] * (Flowey isn't a fan of that name. You feel he may decide to call you something rude instead.) \n/
		\![*]\q[Change it.,TEACHNAME]\n/
		\![*]\q[Risk it.,CANCEL]\n\s[0]\e"
		
	}
	elseif reference0 == username //checks against existing username
	{

		"\0\s[13]\_v[sound_FLOWEY.mp3] * %(username)... Title...?\w4\n\n[half]/
		\![*]\q[Yes,titlechoose]\n/
		\![*]\q[Reject lables,titlenone]\n"
		
		"\0\s[0]\_v[HELP_text.mp3] * (You do not change your name. Are you trying to change something else?) \w4\n\n[half]/
		\![*]\q[Yes,titlechoose]\n/
		\![*]\q[No,titlenone]\n"
		
	}
	else //if the user put in something not specified above, ie a proper name the ghost can use. 
	{
		"\0\s[13]\_v[sound_FLOWEY.mp3] * Title...?\w4\n\n[half]\![*]\q[Reject lables,titlenone]\n"
		"\0\s[0]\_v[HELP_text.mp3] * Do you want to be called by a title? \w4\n\n[half]/
		\![*]\q[Yes,titlechoose]\n/
		\![*]\q[No,titlenone]\n"
		
		
		
	}
}
	
Select.titlechoose
{
	if presuffix == "masculine" //this value was defined in bootend.dic and menu.dic when the user chose their pronouns. Thus, if someone chose he/him pronouns, they would be able to choose Mr. or Master.
		{
		"\![*] \q[Mr.,titlemr]\n/
		\![*] \q[Master,titlemaster]\n"
		}
		
	elseif presuffix == "feminine" //Likewise, if someone chose she/her pronouns, they could choose Ms.,Mrs., or Lady.
		{
		"\![*] \q[Ms.,titlems]\n/
		\![*] \q[Mrs.,titlemrs]\n/
		\![*] \q[Lady,titlelady]\n"
		}
		
	elseif presuffix == "open" // Again, I'm fairly certain this works <3Era
		{
		"\![*] \q[Mr.,titlemr]\n/
		\![*] \q[Master,titlemaster]\n/
		\![*] \q[Ms.,titlems]\n/
		\![*] \q[Mrs.,titlemrs]\n/
		\![*] \q[Lady,titlelady]\n"
		}
	else
		{
			""
		}
	-- //the titles after this are gender neutral and will appear for all users. 
	"\![*] \q[Mx.,titlemx]\n/
	\![*] \q[-ssi,titlessi]\n/
	\![*] \q[-san,titlesan]\n/
	\![*] \q[I changed my mind,titlenone]\s[0]\e"
}
	
Select.titlenone
{
		username = tempname
		NameDone
}

Select.titlemr
{
		username = "Mr. " + tempname
		NameDone
}

Select.titlemaster
{
		username = "Master " + tempname
		NameDone
}
		
Select.titlems
{
		username = "Ms. " + tempname
		NameDone
}

Select.titlemrs
{
		username = "Mrs. " + tempname
		NameDone
}

Select.titlelady
{
		username = "Lady " + tempname
		NameDone
}

Select.titlemx
{
		username = "Mx. " + tempname
		NameDone
}

Select.titlessi
{
		username = tempname + "-ssi"
		NameDone
}

Select.titlesan
{
		username = tempname + "-san"
		NameDone
}

//--NameDone
//Signifies that it's done.

NameDone
{
		"\0\s[6]\_v[sound_FLOWEY.mp3] * \w8%(username)!\s[0]\e"
		
}
