//  ######                                                   
//  #     # #####   ####  #    #  ####  #    # #    #  ####  
//  #     # #    # #    # ##   # #    # #    # ##   # #      
//  ######  #    # #    # # #  # #    # #    # # #  #  ####  
//  #       #####  #    # #  # # #    # #    # #  # #      # 
//  #       #   #  #    # #   ## #    # #    # #   ## #    # 
//  #       #    #  ####  #    #  ####   ####  #    #  ####  

//-------- OnTeachPronouns --------
// This is called from either OnFirstBoot or the User Config menu to set/change the user's pronouns

OnTeachPronouns
{
	"\c\0\s[3]\b2\_v[mus\sound_FLOWEY.mp3] * Do. _o. __.... Pronouns?\w5\n\n\n[half]\_q"
	"\c\0\s[3]\b2\_v[mus\HELP_text.mp3] You are offered a choice of pronouns.\w5\n\n\n[half]\_q"
	--
	
	"\![*] \q[He-His-Him,OnSetPronouns,1]\n/
	\![*] \q[She-Her-Her,OnSetPronouns,2]\n/	
	\![*] \q[They-Their-Them,OnSetPronouns,3]\n/
	\![*] \q[Something else...,OnTeachCustomPronoun]\s[0]"
}	

OnSetPronouns
{
	if reference0 == 1
	{
		nomPro = "he"
		oblPro = "him"
		posPro = "his"
	}
	elseif reference0 == 2
	{
		
		nomPro = "she"
		oblPro = "her"
		posPro = "her"
	}
	elseif reference0 == 3
	{
		
		nomPro = "they"
		oblPro = "them"
		posPro = "their"
	}
	elseif reference0 == 0
	{
		nomPro = reference1
		oblPro = reference2
		posPro = reference3
	}
	--

	"\0\s[3]\_v[mus\HELP_text.mp3] Your pronouns are %(nomPro), %(oblPro), and %(posPro).\n\n F_lowe_y will now call you by the correct pronouns.\n\n You can always change this from f_low_ey's User Configuration Menu."
	--
	
	if firstBoot == 1
	{
		firstBoot = 0
		--
		OnTeachName
	}
	else
	{
		"\e"
	}

}


OnTeachCustomPronoun
{
	"\0\s[3]\_v[mus\HELP_text.mp3] Enter your nominative pronoun, ex.'they'\![open,inputbox,OnNominativeTeach,-1]"
}

//---- Teaching each pronoun ----

OnNominativeTeach
{
	_nounN = reference0 //this stores the pronoun value temporarily in reference0

	if reference0  == "" || reference0 == " " || reference0 == "   " //This is if the user doesn't put in a pronoun at all.
	{
		"\0\s[3]\_v[mus\HELP_text.mp3] You didn't type anything into the box. Whoops! \![open,inputbox,OnNominativeTeach,]"
	}
	else //Continuing on
	{
		"\0\s[3]\_v[mus\HELP_text.mp3] Enter your oblique pronoun, ex.'them'\![open,inputbox,OnObliqueTeach,--reference=%_nounN]"
	}
}


OnObliqueTeach
{
	_nounO = reference0 //this stores the pronoun value temporarily in reference0
	_nounN = reference2
	if reference0  == "" || reference0 == " " || reference0 == "   " //This is if the user doesn't put in a pronoun at all.
	{
		"\0\s[3]\_v[mus\HELP_text.mp3] You didn't type anything into the box. Whoops!\![open,inputbox,OnObliqueTeach,--reference=%_nounN]"
	}
	else //Continuing on
	{
		"\0\s[3]\_v[mus\HELP_text.mp3] Enter your possessive pronoun, ex.'their'\![open,inputbox,OnPossessiveTeach,--reference=%_nounN,--reference=%_nounO]"
	}
}


OnPossessiveTeach
{

	_nounP = reference0 //this stores the pronoun value temporarily in reference0
	_nounO = reference3
	_nounN = reference2
	if reference0  == "" || reference0 == " " || reference0 == "   " //This is if the user doesn't put in a pronoun at all.
	{
		"\0\s[3]\_v[mus\HELP_text.mp3] You didn't type anything into the box. Whoops!\![open,inputbox,OnPossessiveTeach,--reference=%_nounN,--reference=%_nounO]"
	}
	else //Continuing on
	{
		"\0\s[3]\_v[mus\HELP_text.mp3] * You entered %(_nounN), %(_nounO), and %(_nounP).\n * Does that seem right? \w5\n\n[half]\_q/
		\![*]\q[Looks good!,OnSetPronouns,0,%_nounN,%_nounO,%_nounP]\n/
		\![*]\q[Let's try that again...,OnTeachCustomPronoun]"
			
	}
}
	



