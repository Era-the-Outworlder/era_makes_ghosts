//---------------------------Miscellaneous Functions--------------------------





//----Things specifically added by Era----

OnTeach
{
	"OnTeach Test:\n\n%(referenceTest)"
}
//----Moved from other dics----

//---- Username ----
On_username
{
	username
}

//---- Get other active ghost names -------------------------------------------------------
On_otherghostname
{
	// Create a list of currently active ghosts
	ghostexlist = ""
	_i = 0
	while _i >= 0 
	{
		_ghostname = NAMETOVALUE("reference%_i")
		if _ghostname != "" 
		{
			if _i > 0; ghostexlist += ","
			ghostexlist += _ghostname[0]
			_i++
		}
		else; _i = -1
	}
	ghostexcount = ARRAYSIZE(ghostexlist)
}

//---- SHIORI Information ------------------------------------------------------------------
On_version
{
	GETSETTING(AYAINFO_VERSION)
}

On_craftman
{
	"umeici"
}

On_craftmanw
{
	"梅市"
}

On_name
{
	"AYA"
}

//---- Owned SSTP made with uniqueid ------------------------------------------------------
On_uniqueid
{
	uniqueid = reference0
}

//----hwnd Get ------------------------------------------------------------------------
On_hwnd
{
	sakurahwnd    = reference0[0]
	kerohwnd      = reference0[1]
	sakurablnhwnd = reference1[0]
	keroblnhwnd   = reference1[1]
}

// -------- SAORI_CPUID --------
// CPU related
// 
// / absence 
// cpu.clockex High-precision clock frequency (unit: MHz, unit MHz) (Get up to 3 digits after the decimal point)




OnRunCPUID
{
		// OS related
	_platform = FUNCTIONEX("saori_cpuid.dll","platform")
	_os_name = FUNCTIONEX("saori_cpuid.dll","os.name")
	_os_version = FUNCTIONEX("saori_cpuid.dll","os.version")
	_os_version_int = TOINT(_os_version)
	_os_build = FUNCTIONEX("saori_cpuid.dll","os.build")
	
		//CPU related
	_cpu_name = FUNCTIONEX("saori_cpuid.dll","cpu.name")
	_cpu_num = FUNCTIONEX("saori_cpuid.dll","cpu.num")
	_cpu_vender = FUNCTIONEX("saori_cpuid.dll","cpu.vender")
	_cpu_ptype = FUNCTIONEX("saori_cpuid.dll","cpu.ptype")
	_cpu_family = FUNCTIONEX("saori_cpuid.dll","cpu.family") 
	_cpu_model = FUNCTIONEX("saori_cpuid.dll","cpu.model") 
	_cpu_step = FUNCTIONEX("saori_cpuid.dll","cpu.stepping")
	_cpu_mmx = FUNCTIONEX("saori_cpuid.dll","cpu.mmx")  
	_cpu_sse = FUNCTIONEX("saori_cpuid.dll","cpu.sse")  
	_cpu_sse2 = FUNCTIONEX("saori_cpuid.dll","cpu.sse2") 
	_cpu_tdn = FUNCTIONEX("saori_cpuid.dll","cpu.tdn")
	_cpu_htt = FUNCTIONEX("saori_cpuid.dll","cpu.htt")//hyperthreading
	_cpu_clock = FUNCTIONEX("saori_cpuid.dll","cpu.clock")//(unit: MHz)
	_cpu_clockex = FUNCTIONEX("saori_cpuid.dll","cpu.clockex")//(MHz)
	
		// Memory related [MB] t=total a=available
	_mem_usage = FUNCTIONEX("saori_cpuid.dll","mem.os")
	_mem_phyt = FUNCTIONEX("saori_cpuid.dll","mem.phyt")//physical
	_mem_phya = FUNCTIONEX("saori_cpuid.dll","mem.phya")
	_mem_pagt = FUNCTIONEX("saori_cpuid.dll","mem.pagt")//page file
	_mem_paga = FUNCTIONEX("saori_cpuid.dll","mem.paga")
	_mem_virt = FUNCTIONEX("saori_cpuid.dll","mem.virt")//virtual
	_mem_vira = FUNCTIONEX("saori_cpuid.dll","mem.vira")
	"\0\s[3]\b[2]\_q\![set,autoscroll,disable]"
	--
	"OS: %(_os_name) %(_os_version_int) \nBuild:%(_os_build) \n\n"
	--
	"CPU: %(cpu_name)\n%(cpu_clockex)MHz\n\n"
	 // "Num:%(_cpu_num) Vendor:%(_cpu_vender) Ptype:%(_cpu_ptype) Fam:%(_cpu_family) Model:%(_cpu_model) Step:%(_cpu_step) MMX:%(_cpu_mmx) SSE:%(_cpu_sse) SSE2:%(_cpu_sse2) TDN:%(_cpu_tdn) HTT:%(_cpu_htt) Clock:%(_cpu_clock)MHz/"
	--	
	
	 
	"Memory:\n Usage:%(_mem_usage) PhysT%(_mem_phyt) PhysA%(_mem_phya) PageT%(_mem_pagt) PageA%(_mem_paga) VirT%(_mem_virt) VirA%(_mem_vira) "
	--
	"Memory Load: %(memoryload)\n Physical Memory: %(memoryavailphys) KB\n\n"
	--
	"Ukagaka: %property[system.year] %property[baseware.name]"
	--
	"%property[baseware.version]"
	--
	"\e"
}

// -------- Recycling Bin --------

OnRecycleStart
{
	
	_binNum = FUNCTIONEX("gomi.dll", "-n")
	_binSizeB = TOINT(valueex[0])
	_binSizeK = (_binSizeB / 1024); _binSizeM = (_binSizeK / 1024); _binSizeG = (_binSizeM / 1024)
	_type = ""
	_binDisplay = 0
	if _binSizeG >= 5 {_binDisplay = _binSizeG; _type = "GB"}
	elseif _binSizeM >= 100 {_binDisplay = _binSizeM; _type = "MB"}
	elseif _binSizeK >= 100 {_binDisplay = _binSizeK; _type = "KB"}
	else{_binDisplay = _binSizeB; _type = "bytes"}
	--
	if _binNum == 0 //If the recycle bin is empty.
	{
		"\0\s[-1]\w8\w8\w8\w8\s[3]\_v[mus\sound_FLOWEY.mp3]* IT IS EMPTY.\e"
	}
	elseif _binNum == 1 //If the recycle bin has 1 item
	{
		"\0\s[3]\_v[mus\HELP_text.mp3] There is %(_binNum) item, using %(_binDisplay)%(_type) of space. \w8\w8\n/
		 \f[align,center]Proceed?\n	\![*] \f[align,left]\q[Yes,RecycleEmpty]\n \n[-100]\f[align,center]--------\n \n[-100]\f[align,right] \![*] \q[No,RecycleCancel]\n\n[half]\f[align,left]"
	}
	elseif _binNum >= 2 //If the recycle bin has anything in it.
	{
		"\0\s[3]\_v[mus\HELP_text.mp3] There are %(_binNum) items, using %(_binDisplay)%(_type) of space. \w8\w8\n/
		 Proceed?\n/
		\![*] \q[Yes,RecycleEmpty] \n \n[-100]\f[align,center]----\n \n[-100]\f[align,right] \![*] \q[No,RecycleCancel]\n\n[half]\f[align,left]"
	}
}
	

Select.RecycleEmpty
{
	_empty = GLITCH("emptying")
	"\0\s[3]\_v[mus\sound_FLOWEY.mp3]\n * %(_empty)... \w8\w8\s[-1]\w[1000]\![raise,OnRecycleBinEmptied]"	
}

Select.RecycleCancel
{	
	_what = GLITCH("what")
	"\0\s[21]\_v[mus\sound_FLOWEY.mp3]\n * Then what%(_what)WHAT%(_what)what did you do this for?\e"
}

OnRecycleBinEmptied
{	
	if verBalete == "OFF"
	{
		_gomi = FUNCTIONEX("gomi.dll", "-fq")
	}
	else
	{
		_gomi = FUNCTIONEX("gomi.dll", "-ev")
	}
	--
	"\w9\w9\w9\s[6]\_v[mus\sound_FLOWEY.mp3]\n\n * Done."
}

// ----------------------------------------------------------------------------------------
//--- Discord Plugin by steve
//haven't sorted out how it works quite yet

/* 
OnDiscordPluginCustom 
{
	"Connecting to Discord Rich Presence"
	--

'\![raiseplugin,Discord Rich Presence,OnDiscordPluginCustomALL,'
--
''//Appid
--
'What,'//CustomDetail
--
'How'//CustomState
--
''//LargeImageKey
--
'Gaze'//LargeImageText
--

// 'simple_hollow_red_heart,'//SmallImageKey
// --
// 'Developer'//SmallImageText
// --

']'
}
OnDiscordPluginCustomEnd
{
	 "Disonnecting from Discord Rich Presence"
} 
*/

//---- Other Functions----

//--OnFileDrop2

//OnFileDrop2 handles how your ghost will set an image as the desktop background when someone drags and drops an image on them.

OnFileDrop2
{
	_filepath = SPLITPATH(reference0)
	_filename = _filepath[2]
	_fileext = TOLOWER(_filepath[3])
	case _fileext
	{
		when ".jpg", ".jpeg", ".bmp", ".png" //These are the formats your ghost will recognize as image files to set as the background.
		{
			Wallpaperfile = reference0
			
			"\0\s[3]\_v[mus\sound_FLOWEY.mp3] *  FILE %(_filename)%(_fileext). Wallpaper?\n\n[half]/
			\q[Centered,SetWallpaper_0]\n/
			\q[Tiled,SetWallpaper_1]\n/
			\q[Stretched,SetWallpaper_2]\n\n[half]/
			\q[No thanks,SetWallpaper_Cancel]\e"
		}
		when ".mid", ".wav", ".mp3", ".wma", ".au", ".ogg" //Audio file formats
		{
			"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * %(_filename)%(_fileext) is music.\e"
		}
		others //Any other file format. 
		{
			"\0\s[3]\_v[mus\sound_FLOWEY.mp3] *  %(_filename)%(_fileext).\e"
		}
	}
}

//This set of options sets the wallpaper up.

Select.SetWallpaper_0
{
	"\![set,wallpaper,%(Wallpaperfile),center]\0\s[3]\_v[mus\sound_FLOWEY.mp3] *  Centered.\e"
}
Select.SetWallpaper_1
{
	"\![set,wallpaper,%(Wallpaperfile),tile]\0\s[3]\_v[mus\sound_FLOWEY.mp3] *  Tiled.\e"
}
Select.SetWallpaper_2
{
	"\![set,wallpaper,%(Wallpaperfile),stretch]\0\s[3]\_v[mus\sound_FLOWEY.mp3] *  Stretched.\e"
}
Select.SetWallpaper_Cancel
{
	"\0\s[3]\_v[mus\sound_FLOWEY.mp3] *  No. No. No wallpaper.\e"
}


 //---- OnInstallBegin -------------------------------------------------------
 //OnInstallBegin is called when a file with an included install.txt is dropped on your ghost. 
 
OnInstallBegin
 {
	"\0\s[3]\_v[mus\sound_FLOWEY.mp3] *  Contains an install.txt file. Installing.\e"
 }

 //---- OnInstallComplete -------------------------------------------------------
 //OnInstallComplete runs once the file above has finished installing.
 
OnInstallComplete
{
	"\0"
 	--
	if reference0 == "shell" //This is checking to see if what you installed is classified as a shell in the install.txt.
 	{	
		if reference1 == "Flowey" //This is the name of the shell defined in descript.txt in the shell folder.
		{
		"\s[3]\_v[mus\HELP_text.mp3] * (You installed a new shell for a little yellow flower.)\e"
		//Replace if needed.
		}
		else //For any other shell they may install.
		{
		"\s[3]\_v[mus\sound_FLOWEY.mp3] * New shell. New. New. New. New. \w8\0\s[3]Shell: %(reference1).\e"
		//%(reference1) here would be the name of the shell they installed. Replace if needed.
		}
 	}
	elseif reference0 == "ghost" //if the type of the file they're installing is a ghost.
 	{
		"\s[3]\_v[mus\sound_FLOWEY.mp3] * New Ghost.\w8 Installed %(reference1).\e"
		//Reference1 here would be the name of the ghost. Replace as needed.
 	}
	elseif reference0 == "balloon" //if the type of file is a balloon
 	{
		"\s[3]\_v[mus\sound_FLOWEY.mp3] * Balloon \w8 Name: %(reference1).\e"
		//Reference1 here would be the name of the balloon. Replace as needed.
 	}
	elseif reference0 == "plugin" //if the type of file is a plug-in, like a saori of some kind.
 	{
		"\s[3]\_v[mus\sound_FLOWEY.mp3] * Installed plug-in %(reference1).\e"
		//Reference1 here would be the name of the plug-in. Replace as needed.
 	}
}

//---- OnInstallFailure -------------------------------------------------------
//If the file you dropped fails to install for one reason or another.

OnInstallFailure
{
	"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * Failed. Unable to install. \n\n[half]"
	
	--
	if reference0 == "unsupported" //This checks if the reason it failed is because it was unsupported.
	{
	"\s[11]\_v[mus\sound_FLOWEY.mp3] * Can't.\w5"
	"\_v[mus\HELP_text.mp3] * (The filetype was not supported.) \w5"
	}	
	elseif reference0 == "extraction" //This checks if the file didn't extract properly.
	{
	"\s[11]\_v[mus\sound_FLOWEY.mp3] * Unable to extract.\w5"
	"\_v[mus\HELP_text.mp3] * (Unable to extract file.) \w5"
	//See above but for a failed extraction.
	}
}
 
 //--OnInstallRefuse
 //If the file you dropped can't be installed with this ghost.
 
OnInstallRefuse
 {
	"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * %(nonono) \w8Can't can't can't. \w4Try %(reference0).\w8"
	"\0\s[3]\_v[mus\HELP_text.mp3] * (Flowey refused to install the file. Maybe you should try giving it to %(reference0) instead?) \w5"
	
	//reference0 in this function refers to the ghost this file is associated with, probably the name of the ghost listed in its install.txt file. You would see this if you dropped a shell made for a different ghost on your current ghost, for example.
 }


//**** Uninstallation ******************************************
//You may have noticed in the configuration menus in menu.dic an option to turn Uninstall on or off, as well as a greyed out option to uninstall in the right click menu.

//--OnVanishSelecting
//When you select the uninstall option in the menu.

OnVanishSelecting
{
	"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * Whatever. \w8"
	"\0\s[3]\_v[mus\HELP_text.mp3] * (Delete f_lowe_y.exe? Y/N?) \w5"
}

//OnVanishSelected
//When you decide to uninstall them. 

OnVanishSelected
{
	"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * Goodbye %(username). \w8\s[-1]\e"
	"\0\s[3]\_v[mus\HELP_text.mp3] * (Deleting f_lowe_y.exe) \w5\s[-1]\e"
}


//--OnVanishCancel
//If you select Uninstall from the menu, but decide not to do it.

OnVanishCancel
{
	"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * %(thank)\w8\w8\e"
	"\0\s[3]\_v[mus\HELP_text.mp3] * (You did the right thing.) \w5\s[-1]\e"
}

//--OnVanishButtonHold
//This should trigger if you double click the ghost's balloon in the middle of OnVanishSelected above. So it basically cancels their uninstallation. I'd be pretty careful about testing this, and I'd probably just put the line of dialogue into their script input and test it that way instead.

OnVanishButtonHold
{
	"\0\s[3] * ...\8\w8\8\w8\8\w8\e"
}


 //---- OnVanished -----------------------------------------
 //OnVanished is a weird little function. Basically, if you uninstall a ghost, it'll automatically go back to the last ghost you were using. They'll then say this line of dialogue about the ghost you just uninstalled. Make sure to set up both their poses immediately so they both appear when it switches back to them.
 
OnVanished
{
	"\0\s[0]\1\[10] What a shame...\w8\w8\w8\w8\e"
	
	//reference0 here is the name of the other ghost.
}

OnOtherGhostVanished
{
	"\0\s[14]\_v[mus\HELP_text.mp3] * (Is this the last we'll see of %(reference0)?\w8\w8"
	"\0\s[14]\_v[mus\sound_FLOWEY.mp3] * %(reference0) %(reference0) %(reference0) %(reference0)\w8\w8\n\n   Uninstalled."
}


//**** Mail Checking *******************************************************************
//The next set of functions have to do with checking your email.


 //---- OnBIFFBegin ---------------------------------------------------------------
 //When you first tell the ghost to check your email.
 
OnBIFFBegin
 {
	"\0\s[5]\_v[mus\sound_FLOWEY.mp3] * Check_ng... ma_l. \w8\w8\w8\e"
	"\0\s[5]\_v[mus\HELP_text.mp3] * (Flowey is checking your email.) \w8\w8\w8\e"
 }

 
 //---- OnBIFFComplete ---------------------------------------------------------------
 //After they've succeeded in checking your mail, they'll report back with how many new emails you have.
 
OnBIFFComplete
{
	if reference0 == 0 //If you have no new mail.
 	{
 		"\0\s[5]\_v[mus\sound_FLOWEY.mp3] * N_th_ng.. \w8\w8\w8\e"
		"\0\s[5]\_v[mus\HELP_text.mp3] * (You have no new emails.) \w8\w8\w8\e"
	}
	elseif reference0 == 1 //If you have one new email.
 	{
 		"\0\s[5]\_v[mus\sound_FLOWEY.mp3] * 1! N_w _mail. \w8\w8\w8\e"
		"\0\s[5]\_v[mus\HELP_text.mp3] * (You have one new email.) \w8\w8\w8\e"
	}
	elseif reference0 > 1 && reference0 < 11 //If you have more than one email but less than eleven.
	{
		"\0\s[5]\_v[mus\sound_FLOWEY.mp3] * %(reference0)! ne_ ema_ls. \w8\w8\w8\e"
		"\0\s[5]\_v[mus\HELP_text.mp3] * (You have %(reference0) new emails.) \w8\w8\w8\e"
		//reference0 here stores the amount of new email it finds.
	}
	elseif reference0 > 100 //If there are over one hundred new emails.
	{
		"\0\s[5]\_v[mus\sound_FLOWEY.mp3] * %(reference0) emails. \w8\w8\w8\e"
		"\0\s[5]\_v[mus\HELP_text.mp3] * (You have %(reference0) new emails.You should do something about that.) \w8\w8\w8\e"
	}
 	else //For any other number of emails.
	{
		"\0\s[5]\_v[mus\sound_FLOWEY.mp3] * %(reference0) emails. \w8\w8\w8\e"
		"\0\s[5]\_v[mus\HELP_text.mp3] * (%(reference0) new emails.) \w8\w8\w8\e"
 	}
}

 //---- OnBIFFFailure ---------------------------------------------------------------
 //Various error messages that can happen when your ghost tries to connect to an email account.
 
OnBIFFFailure
 {
	if reference0 == "timeout" //If the connection times out.
 	{
		"\0\s[21]\_v[mus\sound_FLOWEY.mp3] * C_nnecti_n timed _ut. \w8\w8\w8\e"
		"\0\s[21]\_v[mus\HELP_text.mp3] * (The connection timed out.) \w8\w8\w8\e"
	}
 	elseif reference0 == "kick" //If the account blocks the ghost from accessing it. I think gmail may do this at first to try and protect your account from unapproved applications using it. It gives you the option to change the setting in the warning email they send you about it.
	{
		"\0\s[21]\_v[mus\sound_FLOWEY.mp3] * Acc_ss d_ni_d. \w8\w8\w8\e"
		"\0\s[21]\_v[mus\HELP_text.mp3] * (Access denied. Check email settings.) \w8\w8\w8\e"
 	}
	elseif reference0 == "defect" //If the information put into the POP menu is wrong.
 	{
		"\0\s[21]\_v[mus\sound_FLOWEY.mp3] * FA_LED. CHECK POP SETT_NGS. \w8\w8\w8\![open,configurationdialog]\e"
		"\0\s[21]\_v[mus\HELP_text.mp3] * (Login failed. You should check your POP settings.) \w8\w8\w8\![open,configurationdialog]\e"
		
		//Note that this ends with \![open,configurationdialog]. This will open up the preferences menu I described above automatically. More on \! functions in the walkthrough. You can delete the \! tag here if you want to.
	}
 	else //if there's some other kind of error.
	{
		"\0\s[21]\_v[mus\sound_FLOWEY.mp3] * FAILED. %(reference[0]) \w8\w8\w8\e"
		"\0\s[21]\_v[mus\HELP_text.mp3] * (Connection failed. %(reference[0])) \w8\w8\w8\e"
		
		//reference0 here stores the error message the email server would have given the ghost, hopefully.
 	}
}


 //**** Network Update *****************************************************************
 //The following functions are for the Network Update of your ghost, where it will connect to your website, check for new files, and update itself.
 
 //You can easily test these messages using the Developer panel. Go to Open at the bottom of it and choose one of the four Update Event options to simulate a Ghost's update process.
 
 //---- OnUpdateBegin -------------------------------------------------------------
 //When you first tell your ghost to update. Note that they end with \w8 to give a little breather time.
 
OnUpdateBegin
 {
	"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * U_dating\w5.\w5.\w5. \w8\w8\w8\e"
	"\0\s[3]\_v[mus\HELP_text.mp3] * (Flowey is checking for updates.) \w8\w8\w8\e"
 }

 //---- OnUpdateReady ---------------------------------------------------------
 //If the ghost finds new files to update with.
 
OnUpdateReady
 {
 
	reference0++ //makes sure it starts at 1 instead of 0
	if reference0 == 1
	{
		"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * %(reference0) F_LE AVA_LABLE. \w8\w8\w8"
		"\0\s[3]\_v[mus\HELP_text.mp3] * (%(reference0) new file available for update.) \w8\w8\w8"
	
	}
	else
	{
		"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * %(reference0) F_LES AVA_LABLE. \w8\w8\w8"
		"\0\s[3]\_v[mus\HELP_text.mp3] * (%(reference0) new files available for update.) \w8\w8\w8"
	
	}
    --
	"\c\0\s[26]\i[9]\_q * DOWNLOADING\w5.\w5.\w5.\n\n"
	//reference0 here storing the number of new files. You don't have to use it if you don't want to.
 }





 //---- OnUpdate.OnDownloadBegin ------------------------------------------------
 //This dialogue sets up the cool progress bar. I think it's real neat and since I plan on using this trimmed down version I'm keeping it in here.
 
 //reference0 - file being downloaded
 //reference1 - number of file being downloaded, as in "this is file 1 out of 9"
 //reference2 - overall number of files to download, counting zero. so if there are ten files, it'll display as 9.
 //reference3 - what's being updated - ghost, shell, balloon, plugin, headline, baseware

 //Big thanks to Malakin for helping me figure it out.
 
 
//---- Downloading the update ------------------------------------------------
OnUpdate.OnDownloadBegin
 {
		"\C"
		 --
		 
		Update_Process = TOINT(reference1) + 1
		Update_Total = TOINT(reference2) + 1
		Update_Remaining = Update_Total - Update_Process
		
		//defining starting points
		PointA = 1 
		PointB = 1
		
		//defining text buffers beforehand, otherwise they don't display right
		_txt = ""
		
		//loop calculates how many check boxes to display based on the variables above, the if check is for displaying the ascii border on the sides
		while PointA <= Update_Process 
		{
		_txt += "☑" 
		PointA++ 
		} 
		//see above except for blank boxes
		while PointB <= Update_Remaining 
		{
		_txt += "☐" 
		PointB++ 
		}
		--
		//using the text buffers to display the correct number of boxes
		"\c[line,2]%(_txt)\n\n%(Update_Process) of %(Update_Total)"
		--
		//flavortext - i don't want to lose this, i hope there's a way i can keep all of it
		
		 	if "aitalk" _in_ reference0 //this is checking to see what kind of files are being downloaded. In this case, it's looking for files called aitalk in the download queue.
			{
				"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * D_wnl_ading new w_rds. \w8\w8\w8\e"
				"\0\s[3]\_v[mus\HELP_text.mp3] * (Flowey is downloading new dialogue.) \w8\w8\w8\e"
			}
			else //for files not specified above
			{
			""
			} 
		
		--
		"\w5\e"
 }

 //---- OnUpdate.OnMD5CompareBegin ---------------------------------------------------
 //This checks the hash value of the files in the directory and the files listed in updates.dau, I believe.
 
OnUpdate.OnMD5CompareBegin
 {
	//"\0\s[3] * Checking the MD5 value.\e"
 }

 //---- OnUpdate.OnMD5CompareComplete --------------------------------------------------
 //After they've checked the MD5 value and it matches.
 
OnUpdate.OnMD5CompareComplete
 {
	//"\0\s[3] * MD5 check good.\e"
}

 //---- OnUpdate.OnMD5CompareFailure ---------------------------------------------------
 //If the MD5 values don't match, meaning if the file is missing or damaged or incomplete in some way.
 
OnUpdate.OnMD5CompareFailure
 {
	_file = SPLITPATH(reference1)[2] + SPLITPATH(reference1)[3]
	"\0\s[3] The MD5 values don't match. Source: %(_file)\w8\e"
 }


//---- OnUpdateComplete -------------------------------------------------------------
//When the ghost has finished the update process.

OnUpdateComplete
 {
	if reference0 == "none" //if the ghost had nothing to update with
 	{
 		"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * NOTHING CHANGED. \w8\w8\w8\e"
		"\0\s[3]\_v[mus\HELP_text.mp3] * NOTHING CHANGED. \w8\w8\w8\e"
		
	}
 	else //if the ghost has updated successfully
	{
		"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * SOMETHING CHANGED. VIEW CHANGELOG?\w8"
		"\0\s[3]\_v[mus\HELP_text.mp3] * SOMETHING CHANGED. VIEW CHANGELOG?\w8"
		--
		"\n\n\f[align,left] \![*] \q[Yes,OnChangeLog,update]\n \n[-100]\f[align,right]\![*] \q[No,NoChangelog]"
 	}
}


Select.NoChangelog
{
	
		"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * View from Menu. Change Any time \w8\w8\w8\e"
		"\0\s[3]\_v[mus\HELP_text.mp3] * (The changelog may be viewed at any time from the functions menu.) \w8\w8\w8\e"
}


 //---- OnUpdateFailure -------------------------------------------------------------
 //Error messages if the update process fails in some way.
 
OnUpdateFailure
{
	_file = SPLITPATH(reference1)[2] + SPLITPATH(reference1)[3]
	if reference0 == "timeout" //if the connection times out
 	{
 		"\0\s[21]\_v[mus\sound_FLOWEY.mp3] * f_AILED. C_onnection l_ost \w8\w8\w8\e"
		"\0\s[21]\_v[mus\HELP_text.mp3] The update failed because the connection timed out. \w8\w8\w8\e"
	}
 	elseif reference0 == "md5 miss" 
	{
		"\0\s[17]\_v[mus\sound_FLOWEY.mp3] * HASH HASH HASH ##### MD5 hash check failed. \w8\w8\w8\e"
		"\0\s[17]\_v[mus\HELP_text.mp3] NOTHING CHANGED. MD5 MISMATCH IN FILE %(_file) \w8\w8\w8\e"
 	}
	else //for other errors
 	{
		"\0\s[21]\_v[mus\sound_FLOWEY.mp3] * f_AILED. ERROR: %(reference0) SOURCE: %(_file) \w8\w8\w8\e"
		"\0\s[21]\_v[mus\HELP_text.mp3] Update failed. Looks like ERROR:%(reference0).\n Source: %(_file). \w8\w8\w8\e"
	}
}
 
 
 //**** Headlines and RSS Feeds *********************************************************
 //This section has to do with the RSS feeds option on your ghost. If you drag and drop a url on them, they'll add it to a list you can access via the right-click menu, and they'll tell you the last set of updates for the site. This works best with RSS feeds I think, but you can experiment with other sites to see how it works.
 //The headlines you see under the tab are in the headline folder under your base SSP install. So it'd be SSP\headlines. You should see the folders for each linked headline in there. Likewise, your RSS feeds you've added will be under SSP\rss and I don't think that option will appear in the headlines/RSS tab until you add an RSS feed to a ghost. If you want to clear your RSS feeds or headlines, just delete either of those two folders.
 

 //---- OnHeadlinesenseBegin -----------------------------------------------------------
 //This dialogue comes up when you select something in your RSS feed as an intro, like the other On[Thing]Begin things we've seen so far.
 
OnHeadlinesenseBegin
 {
	"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * Selected %(reference0).\e"
 }

 //---- OnHeadlinesense.OnFind --------------------------------------------------------
 //This actually displays the feed.
 
OnHeadlinesense.OnFind
{
	//----  What page You're on
 	if reference2 == "First" || reference2 == "First and Last"
	{
 		//---- First page
		"\0\b2\s[3]%(reference0):"
 	}
	elseif reference2 == "Next"
 	{
		//---- Second page
 		"\0\b2\s[3]%(reference0):"
	}
 	elseif reference2 == "Last"
	{
 		//---- Last page
		"\0\b2\s[3]%(reference0):"
 	}
	--
 	//---- Headlines
	"\n\n%reference3\n"
 	--
	//---- Pages
 	if reference2 == "First" || reference2 == "Next"
	{
 		//---- Bottom buttons
		"\![*] \q[Next,] - \![*] \q[Stop,Headline_Cancel]\e"
 	}
	else
 	{
		//---- Bottom buttons
 		"\![*] \q[Stop,Headline_Cancel]\e"
 	}
	
}


Select.Headline_Cancel
{
	"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * DONE. \w8\w8\w8\e"
	"\0\s[3]\_v[mus\HELP_text.mp3] * (Flowey closed the feed) \w8\w8\w8\e"
}

 //---- OnHeadlinesenseComplete -------------------------------------------------------
 //This is an odd function. As far as I can gather, this is if the headline information gathering was successful, but there's no new information to report. Possibly if none of the feeds have updated? I'm a little fuzzy on this one.
 
OnHeadlinesenseComplete
 {
	"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * DONE. %(reference0)\w8\w8\w8\e"
	"\0\s[3]\_v[mus\HELP_text.mp3] * (%(reference0)) \w8\w8\w8\e"
 }

 //---- OnHeadlinesenseFailure ---------------------------------------------------------
 //This one is more clearly for when the ghost can't get the information it needs about the RSS feed in question.
 
OnHeadlinesenseFailure
  {
if RAND(100) <51
	{
		"\0\s[21]\_v[mus\sound_FLOWEY.mp3] * FAILURE. \w8\n\n[half]"
	
 	--
		if reference0 == "can't download" //if they can't download the rss feed for some reason
		{
			"\_v[mus\sound_FLOWEY.mp3] * C_NNOT DOWNLO_D \w8\w8\s[3]"
		}
		elseif reference0 == "can't analyze" //if they can't read the file for some reason
		{
			"\_v[mus\sound_FLOWEY.mp3] * ANA_YSIS FAI_ED \w8\s[3]"
		}
		else //other errors
		{
			"\_v[mus\sound_FLOWEY.mp3] * HEADLINESENSE ERROR %(errorMsg)\w8\s[3]"
		}
	--
		"\e" //And here is the final /e, so you don't need to include it in the if statements above. Another one of the very rare cases this occurs.
	}
	else
	{
		"\0\s[21]\_v[mus\HELP_text.mp3] * %(errorMsg)\w8\n\n[half]"	
		
	--
		if reference0 == "can't download" //if they can't download the rss feed for some reason
		{
			"\_v[mus\HELP_text.mp3] * (Flowey could not download the feed... Strange.)\w8\s[3]"
		}
		elseif reference0 == "can't analyze" //if they can't read the file for some reason
		{
			"\_v[mus\HELP_text.mp3] * (Flowey couldn't read the files. Weird...)\w8\s[3]"
		}
		else //other errors
		{
			"\_v[mus\HELP_text.mp3] * Unknown error. %(errorMsg)\w8\s[3]"
		}
	--
		"\e"
	}
}


//**** Ghost Creation and Updating ******************************************************
//These next few are related to creating updates2.dau and .nar files for your ghosts.


 //---- OnUpdatedataCreating -----------------------------------------------------------
 //This will begin the creation of the updates2.dau file.
 
OnUpdatedataCreating
 {
	"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * Updates... dau dau dau...\w8\w8\w8\e"
	"\0\s[3]\_v[mus\HELP_text.mp3] * (The updates2.dau file is being generated) \w8\w8\w8\e"
 }

 //---- OnUpdatedataCreated -------------------------------------------------------------
 //After the updates2.dau file has been successfully created.
 
OnUpdatedataCreated
 {
	"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * Done.\w8\w8\w8\e"
	"\0\s[3]\_v[mus\HELP_text.mp3] * (The deed is done.) \w8\w8\w8\e"
 }

 //---- OnNarCreating ------------------------------------------------------------------
 //Along the same lines, this is when they begin creating a .nar file for your ghost.
 
OnNarCreating
 {
	"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * .NAR RAN.\w8\w8\w8\e"
	"\0\s[3]\_v[mus\HELP_text.mp3] * (Creating .nar file.) \w8\w8\w8\e"
 }

 //---- OnNarCreated -------------------------------------------------------------------
 //After the nar file has been successfully created.
 
OnNarCreated
 {
	"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * %(reference0).Do_e.\w8\w8\w8\e"
	"\0\s[3]\_v[mus\HELP_text.mp3] * (%(reference0) created.) \w8\w8\w8\e"
 }

 //****　Clock Synchronization　**********************************************************
//**** 미카 (http://homepage2.nifty.com/ko-ki/)의 사전을 참고했습니다．감사＞こ 나무 성
// Machine translation: "I referenced the dictionary of Mika (http://homepage2.nifty.com/ko-ki/). Thank you. Namu Castle. "

//This set of functions was done by the above Korean coder, I think. I'll leave their comment there untouched. 
 

 //----OnSNTPBegin ------------------------------------------------------------------
 //As with the others you've seen above, this is the beginning bit of dialogue for this process.
 
OnSNTPBegin
 {
	"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * TIME. time.\w8\e"
	"\0\s[3]\_v[mus\HELP_text.mp3] * (Flowey is checking the time.) \w8\e"
	//%(reference0) here is the site they'll be using to check your clock.
 }


 //----　OnSNTPCompare----------------------------------------------------------
 //Now the ghost is going to compare the current time of your computer's system to the time of the site they connected to. 
 
OnSNTPCompare
 {
 	"Retrieved Time: %reference1[1]/%reference1[2]/%reference1[0] %reference1[3]:%reference1[4]:%reference1[5]\w2\n/
	Your Time: %reference2[1]/%reference2[2]/%reference2[0] %reference2[3]:%reference2[4]:%reference2[5]\w2\n/
 	\n"
	--
 	if reference3 == 0 //if your clock is already accurate.
	{
 		"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * TIME. CORRECT. \w8\e"
		"\0\s[3]\_v[mus\HELP_text.mp3] * (The time is correct.) \w8\e"
	}
 	else //if your clock doesn't match
	{
 		"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * WRONG. \w6\s[3]\n * FIX?\n\n[half] \![*] \q[Yes,OnSNTPEXECUTE]\n \n[-100]\f[align,right]\![*] \q[No,OnSNTPCANCEL]\e"
		
		"\0\s[3]\_v[mus\HELP_text.mp3] * (Your clock is wrong.}\w6\s[3]\n * (You are given the option of correcting it.)\n\n[half] \![*] \q[Correct it,OnSNTPEXECUTE]\n \n[-100]\f[align,right]\![*] \q[Leave it,OnSNTPCANCEL]\e"
	}
 }

 //----　OnSNTPEXECUTE ------------------------------------------------------------------
 //After they've fixed your clock. Note the \6 tag - that's what actually changes the clock. Do not remove that tag.
 
OnSNTPEXECUTE
 {
	"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * Corrected. \w8\e"
	"\0\s[3]\_v[mus\HELP_text.mp3] * (The time is now correct.) \w8\e"
 }

 //----　OnSNTPCANCEL ------------------------------------------------------------------
 //If you decide not to change your clock when they tell you it doesn't match.
 
OnSNTPCANCEL
 {
	"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * I-i-i-i-idi- Moron. \w8\e"
	"\0\s[3]\_v[mus\HELP_text.mp3] * (You decide that you like being wrong.) \w8\e"
 }

 //----　OnSNTPFailure ------------------------------------------------------------------
 //If they can't change the clock for some reason, possibly because the site timed out but it could be anything.
 
OnSNTPFailure
 {
	"\0\s[14]\_v[mus\sound_FLOWEY.mp3] * Tried... failed... \w8\e"
	"\0\s[14]\_v[mus\HELP_text.mp3] * (It refused.) \w8\e"
 }


 //--OnDisplayChange----------------------------------------------------------------
 //This is when you change the resolution of your monitor while your ghost is running.
 
OnDisplayChange
 {
	displaybpp    = reference0
 	displaywidth  = reference1
	displayheight = reference2
 }



 //---- OnScreenSaverStart -------------------------------------------------------
 //This happens if your ghost is running when your screensaver starts up. I've never got this to trigger but I love this idea, and I feel like there's a way to do it I haven't figured out yet. So you can skip these for now if you want, but if I do ever get it working, I'll come back and make a note of it.
 
OnScreenSaverStart
 {
	"\0\s[14]\_v[mus\sound_FLOWEY.mp3] * Ha... ha... \w8\e"
	"\0\s[14]\_v[mus\HELP_text.mp3] * (The world is strange.) \w8\e"
 }

 //---- OnScreenSaverEnd -------------------------------------------------------
 //When you wiggle your mouse and stop your screensaver.
 
OnScreenSaverEnd
 {
	"\0\s[5]\_v[mus\sound_FLOWEY.mp3] * Stopped... good. \w8\e"
	"\0\s[5]\_v[mus\HELP_text.mp3] * (The world returns to 'normal'.) \w8\e"
 }


 //---- OnApplicationBoot ---------------------------------------------------------------
 //This supposedly is for when a program starts but I have honestly never gotten it to work or seen it happen. I wouldn't sweat these three related functions too much.
 
OnApplicationBoot
 {
	"\0\s[5]\_v[mus\sound_FLOWEY.mp3] * %(reference0) started. \w8\e"
	"\0\s[5]\_v[mus\HELP_text.mp3] * (%(reference0) started.) \w8\e"
 }

 //---- OnApplicationClose --------------------------------------------------------------
 //See above, but for the program closing.
 
OnApplicationClose
 {
	"\0\s[5]\_v[mus\sound_FLOWEY.mp3] * %(reference0) closed. \w8\e"
	"\0\s[5]\_v[mus\HELP_text.mp3] * (%(reference0) closed.) \w8\e"
 }

 //---- OnApplicationFileOpen ----------------------------------------------------------
 //I believe this is for when the program detailed above opens a document file of some kind.
 
OnApplicationFileOpen
 {
	"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * O_ening %(reference0). \w8\e"
	"\0\s[3]\_v[mus\HELP_text.mp3] * (%(reference0) is being opened.) \w8\e"
 }




 //---- OnBatteryLow -----------------------------------------------------------------
 //This is for when your laptop's battery is low.
 
OnBatteryLow
 {
	"\0\s[5]\_v[mus\sound_FLOWEY.mp3] * %(reference0) pow_r r_maining. \w8\e"
	"\0\s[5]\_v[mus\HELP_text.mp3] * (You see your battery is low. Only %(reference0) power remains.) \w8\e"
 }
 
 //--OnBatteryCritical-----------------------------------------------------------------
 //Likewise, this is for if your battery is REALLY low.
 
OnBatteryCritical
{
	"\0\s[21]\_v[mus\sound_FLOWEY.mp3] * Pow_r critical. \w8\e"
	"\0\s[21]\_v[mus\HELP_text.mp3] * (Flowey thinks you're an idiot for letting your battery get this low.) \w8\e"
}
 
 
 //--OnShellScaling
 //If you right click the ghost, you can go to Shell Scaling, which lists a number of different percentages. Those are the percentages your ghost can be scaled in size up or down.
 
OnShellScaling
{
	if reference0 >= 150 //this number is the percentage that you're scaling the ghost up to. In this case, it's 150%.
	{
		"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * %(hahaha)\e"
		"\0\s[16]\_v[mus\HELP_text.mp3] * (Making Flowey larger... was that a good idea?) \w8\e"
	}
	elseif reference0 == 100 //this is 100%
	{
		"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * Normality.\e"
		"\0\s[16]\_v[mus\HELP_text.mp3] * (Better this way.) \w8\e"
	}
	elseif reference0 == 50 //this is 50%. You can add any percentage that's listed under shell scaling if you want using more elseifs.
	{
		"\0\s[3]\_v[mus\sound_FLOWEY.mp3] * Pat pat pat patpatpatpathetic.\w8\w8"
		"\0\s[16]\_v[mus\HELP_text.mp3] * (Smol and cute.) \w8\e"
	}
}