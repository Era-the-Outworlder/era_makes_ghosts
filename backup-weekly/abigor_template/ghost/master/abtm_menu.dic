
// [..       [..[........[...     [..[..     [..
// [. [..   [...[..      [. [..   [..[..     [..
// [.. [.. [ [..[..      [.. [..  [..[..     [..
// [..  [..  [..[......  [..  [.. [..[..     [..
// [..   [.  [..[..      [..   [. [..[..     [..
// [..       [..[..      [..    [. ..[..     [..
// [..       [..[........[..      [..  [.....   

// ----- Menus -----
//This .dic file is primarily concerned with the double-click menu.


// ----- OnMainMenu -----


OnMainMenu
{	
	_source = _argv[0]
	if (_source == "click1") || (reference[0] == "menu1")
	{
		"\0\b[2]\![set,autoscroll,disable]This menu is opened from the main character.\n\_q"
	}
	elseif (_source == "click2") || (reference[0] == "menu2")
	{
		"\1\b[2]\![set,autoscroll,disable]This menu is opened from the secondary character.\n\_q"
	}
	--
	"\![*] \q[User Settings,OnUserConfig]\n"
	--
	"\![*] \q[Ghost Settings,OnConfigMenu]\n"
	--
	"\![*] \q[Other Functions,OnFunctionMenu]\n[150]"
	--
	"%(disDate) %(disTime)\n"
	--
	"v%(OnGhostVer)\n"
	--
	if lastUpDate
	{
		"Last Updated: %(lastUpDate)\n"
	}
	else
	{
		"\n"
	}
	--
	if devDebug == "ON"
	{
		"\![*] \q[Test variable,OnTestVariable]\n"
		--
		"\![*] \q[Close Menu,CANCEL]\e"
	}
	else
	{
		"\n\![*] \q[Close Menu,CANCEL]\e"
	}
}


// ----- OnUserConfig -----
// This menu allows the user to change their name and other information

OnUserConfig
{
	"\0\b[2]\![set,autoscroll,disable]\_q" 
	--
	"\f[height,-2]\![*](\q[Edit,OnTeachName,menu])\f[height,+2]NAME:\f[height,default]\f[color,ccffff] %(username)\f[default]\n"
	--
	"\f[height,-2]\![*](\q[Edit,OnTeachPronouns,menu])\f[height,+2]PRONOUNS:\f[height,default] \n\f[color,bbffff]%(nomPro)/%(oblPro)/%(posPro)\f[default]\n"
	--
	"\f[height,-2]\![*](\q[Edit,OnGetBirthday,menu])\f[height,+2]BIRTHDAY:\f[height,default] \n\f[color,aaffff]%(birthdayprint)\f[default]\n"
	--
	"\![*] \q[GHOST Settings,OnConfigMenu]\n"
	--	
	"\![*] \q[Main Menu,OnMainMenu]\n"
	--
 	"\![*] \q[Close,CANCEL]"
	--
	"\e"
}

// ----- OnConfigMenu -----
// Allows user to change various ghost settings
OnConfigMenu
{
	"\b[2]\![set,autoscroll,disable]\_q" 
	--
	"\![*]\q[TALK RATE:,OnSetTalk] %(talksetting) \f[size,-2]Commu:%(communicateratio)%\f[default]\n[150]"
	--
	
	"ENABLE UNINSTALL?\n "
	--
	if deleteshitai == "ON"
	{
		"\f[align,center]\f[color,#0099cc]\![*]\q[ON,OnConfigSet,deleteon]\f[color,default] ----- \![*]\q[OFF,OnConfigSet,deleteoff]\n \f[align,left]"
	}
	else
	{
		"\f[align,center]\![*]\q[ON,OnConfigSet,deleteon] ----- \f[color,#0099cc]\![*]\q[OFF,OnConfigSet,deleteoff]\f[color,default]\n \f[align,left]"
	}
	--
	
	"ENABLE SILENT MODE?\n "
	--
	if silentMode == "ON"
	{
		"\f[align,center]\f[color,#0099cc]\![*]\q[ON,OnConfigSet,silenton]\f[color,default] ----- \![*]\q[OFF,OnConfigSet,silentoff]\n \f[align,left]"
	}
	else
	{
		"\f[align,center]\![*]\q[ON,OnConfigSet,silenton] ----- \f[color,#0099cc]\![*]\q[OFF,OnConfigSet,silentoff]\f[color,default]\n \f[align,left]"
	}
	--
	
	"CONFIRM RECYCLE BIN?\n "
	--
	if verBalete == "ON"
	{
		"\f[align,center]\f[color,#0099cc]\![*]\q[ON,OnConfigSet,verBinOn]\f[color,default] ----- \![*]\q[OFF,OnConfigSet,verBinOff]\n \f[align,left]"
	}
	else
	{
		"\f[align,center]\![*]\q[ON,OnConfigSet,verBinOn] ----- \f[color,#0099cc]\![*]\q[OFF,OnConfigSet,verBinOff]\f[color,default]\n \f[align,left]"
	}
	--

	"\n[half]\![*] \q[User Settings,OnUserConfig]\n"
	--
	"\f[align,center]\![*] \q[Main Menu,OnMainMenu,return] ---------- \![*] \q[Close,CANCEL]"
	--
	"\e"
}

OnConfigSet
{
	case reference[0]
	{
		when "deleteon" // Enable/Disable Uninstall
		{
			deleteshitai = "ON"
		}
		when "deleteoff"
		{
			deleteshitai = "OFF"
		}
		when "verBinOn" // Enable/Disable Verbose emptying recycle bin
		{
			verBalete = "ON"
		}
		when "verBinOff"
		{
			verBalete = "OFF"
		}
		when "silenton"	// Enable/Disable SilentMode
		{
			silentMode = "ON"
		}
		when "silentoff"
		{
			silentMode = "OFF"
		}
	}
	--
	OnConfigMenu
}


// ----- Talk Rate -----
// This is when the user chooses to adjust the talk rate in the config menu. The talk rate is how often the ghost will read some dialogue from aitalk.dic.

OnSetTalk
{
	"\b2This is where the talk timing is set\_q"
	--
	"\![*] \q[ Don't Talk,OnTalkSet,never]\n/
	\![*] \q[ Every 15 minutes,OnTalkSet,fifteen]\n/
	\![*] \q[ Every 30 minutes,OnTalkSet,thirty]\n"
	--
	'\![*] \q[ Custom Time,"script:\![open,sliderinput,OnTalkSet,,%(minutetalk),0,60]"]\n'
	--
	"\![*] \q[ Random Time,OnTalkSet,random]\n[150]/
	\![*] \q[ Don't change,OnTalkSet,stay]"
}

OnTalkSet
{
	"I will talk"
	--
	case reference0
	{
		when "never"
		{
			minutetalk = 0
			talksetting = "never"
			aitalkinterval = 0
			
			"... Never, actually."
		}
		when "fifteen"
		{
			minutetalk = 15
			talksetting = "15 minutes"
			aitalkinterval = 900
			
			" every 15 minutes."
		}
		when "thirty"
		{
			minutetalk = 30
			talksetting = "30 minutes"
			aitalkinterval = 1800
			
			" every 30 minutes."
		}
		when "random"
		{
			minutetalk = (RAND(60) + 1)
			talksetting = "%(minutetalk) minutes"
			aitalkinterval = (minutetalk * 60)
			
			" every %(minutetalk) minutes."
		}
		when "stay"
		{
			"I will do as I was before, then.."
		}
		others
		{
			minutetalk = reference0
			if minutetalk != 0
			{
				talksetting = "%(minutetalk) minutes"
				aitalkinterval = (minutetalk * 60)
				
				" every %(minutetalk) minutes."
			}
			else
			{
				talksetting = "never"
				aitalkinterval = 0
				
				"... Never, actually. Strange..."
			}
		}
	}
	--
	"\n[200]Would you like me to try to talk to other ghosts, and if so what percentage of my speech?\n"
	--
	"\f[align,center]\q[No,OnCommuSet,0] ------ "
	--
	'\q[Yes,"script:\![open,sliderinput,OnCommuSet,,%(communicateratio),0,100]"]'
}

OnCommuSet
{
	communicateratio = reference0
	"I will talk to other ghosts %(communicateratio)% of the time."
}

OnFunctionMenu
{

	"\b2Listing Functions: \n[200]"
	
	--
	'\![*] \q[Open Calendar,"script:Opening Calendar.\![open,calendar]"]\n'
	--
	'\![*] \q[Check Email,"script:\![biff]"]\n'
	--
	'\![*] \q[Open SSP Preferences,"script:Opening the SSP preferences menu.\![open,configurationdialog]"]\n'
	--
	"\![*] \q[Empty Recycle Bin,OnRecycleStart]\n"
	--
	"\![*] \q[System Information,OnRunCPUID]\n"
	--
	"\![*] \q[View Ghost Changelog,OnChangeLog,menu]\n"
	--	
	"\![*] \q[Main Menu,OnMainMenu,return]\n"
	--
	if devDebug == "ON"
	{
		"\f[align,center]\![*] \q[Test variable,OnTestVariable] \![*]"
	}
	else
	{
		"\e"
	}
}
