//---------------------------Menus--------------------------
//This .dic file is primarily concerned with the double-click menus for each of your ghosts.



// ----OpenSakuraMenu ----
// This is a bit of intro dialogue before the main character opens their menu.

OpenSakuraMenu
{
		
		"\b2\![set,autoscroll,disable]%(menuIntro)"
		--
		OnMenuRun
}

// ---- OnMenuRun ----
// As you just saw above. This actually fills out the rest of the menu.

OnMenuRun
{	
	"\b2\n[110]\_q" 
	--
	if lastTalk != ""
	{
		"\![*] \q[%(rlTalk),OnLastTalk]\n"
	}
	else
	{
		"\n"
	}
	--
 	"\![*] \q[%(newTalk),OnAiTalk]\n"
	--
 	"\![*] \q[%(ghostChat),OnStartCommunicate]\n"
	--
	"\![*] \q[%(userMenu),OnUserConfig]\n"
	--
	"\![*] \q[%(configMenu),OnConfigMenu]\n"
	--
	"\![*] \q[%(funMenu),OnFunctionMenu]\n[200]"
	--
	"\![*] \q[%(statsMenu),OnStatMenu]\n"
	--
	"LV: %(ellVee)\n[200]"
	--
	"%(disDate) %(disTime)\n"
	--
	"v%(OnGhostVer)\n"
	--
	if lastUpDate
	{
		"Last Updated: %(lastUpDate)\n[150]"
	}
	else
	{
		"\n[150]"
	}
	--
 	"\![*] \q[%(closeMenu),CANCEL]\n[150]"
	--
	
	if eraDebug == "ON"
	{
		"\![*] \q[Test variable,OnTestVariable]\n\e"
	}
	else
	{
		"\e"
	}
}



OnLastTalk 
{
	"%(lastTalk)\e"
}


// -------- OnUserConfig --------
// This menu allows the user to change their name and other information

OnUserConfig
{
	"\0\b[2]\![set,autoscroll,disable]\_q\n" 
	--
	"\f[height,17]NAME:\f[height,default] %(username) \n/
	\f[height,15]\![*](\q[%(change)?,OnTeachName,menu])\f[height,default]\n"
	--
	"\f[height,17]PRONOUNS:\f[height,default]\n%(nomPro)/%(oblPro)/%(posPro) \n/
	\f[height,15]\![*](\q[%(change)?,OnTeachPronouns,menu])\f[height,default]\n"
	--
	"\f[height,17]BIRTHDAY:\f[height,default]\n%(birthdayprint), %(menuBDay) \n/
	\f[height,15]\![*](\q[%(change)?,OnGetBirthday,menu])\f[height,default]\n"
	--
	if username == "Era" 
	{
		"Set Debug?\n(NOW : %(eraDebug))\n\![*] \q[ON,OnSetEraDebug,eDBon]\n \n[-100]\f[align,center]----\n \n[-100]\f[align,right] \![*] \q[OFF,OnSetEraDebug,eDBoff]\n[150]\f[align,left]"
	}
	--
	"\![*] \q[%(configMenu),OnConfigMenu]\n"
	--	
	"\![*] \q[%(backMenu),OnMenuRun]\n"
	--
 	"\![*] \q[%(closeMenu),CANCEL]\e"
}

OnSetEraDebug
{
	case reference[0]
	{
		when "eDBon" // Private Debug Mode, nothing interesting here
		{
			eraDebug = "ON"
		}
		when "eDBoff"
		{
			eraDebug = "OFF"
		}
	}
	--
	OnUserConfig
}


// ---- OnConfigMenu ----
// Allows user to change various ghost settings
OnConfigMenu
{
	"\0\b[2]\![set,autoscroll,disable]\_q\n" 
	--
	"TALK RATE: \nevery %(talktime) \n\f[height,14]\![*] (\q[%(change),OnSetTalk])\f[height,default]\n[150]"
	--
	"Discord Rich Presence \n"
	--
	if InDiscordPluginCustom == 1
	{
		"\f[color,#fb0300]\![*] \q[ON,OnDiscordPluginCustom] \f[color,default]\n \n[-100]\f[align,center]----\n \n[-100]\f[align,right] \![*] \q[OFF,OnDiscordPluginCustomEnd]\n[150]\f[align,left]"
	}
	else
	{
		"\![*] \q[ON,OnDiscordPluginCustom] \n \n[-100]\f[align,center]----\n \n[-100]\f[align,right] \f[color,#fb0300]\![*] \q[OFF,OnDiscordPluginCustomEnd]\f[color,default]\n\f[align,left]"
	}
	--
	"\q[Open DRP Menu,OnDRPMenu]\n[150]"
	--
	"Enable Silent Mode?\n"
	--
	if silentMode == "ON"
	{
		"\f[color,#fb0300]\![*] \q[ON,OnConfigSet,silenton] \f[color,default]\n \n[-100]\f[align,center]----\n \n[-100]\f[align,right] \![*] \q[OFF,OnConfigSet,silentoff] \n[150]\f[align,left]"
	}
	else
	{
		"\![*] \q[ON,OnConfigSet,silenton] \n \n[-100]\f[align,center]----\n \n[-100]\f[align,right] \f[color,#fb0300]\![*] \q[OFF,OnConfigSet,silentoff]\f[color,default]\n[150]\f[align,left]"
	}
	--
	"Confirm Recycle Bin?\n"
	--
	if verBalete == "ON"
	{
		"\f[color,#fb0300]\![*] \q[ON,OnConfigSet,verBinOn] \f[color,default]\n \n[-100]\f[align,center]----\n \n[-100]\f[align,right] \![*] \q[OFF,OnConfigSet,verBinOff]\n[150]\f[align,left]"
	}
	else
	{
		"\![*] \q[ON,OnConfigSet,verBinOn] \n \n[-100]\f[align,center]----\n \n[-100]\f[align,right] \f[color,#fb0300]\![*] \q[OFF,OnConfigSet,verBinOff]\f[color,default]\n[150]\f[align,left]"
	}
	--		
	// "Program Commentary?\n"
	// --
	// if canDesktopEye == "ON"
	// {
		// "\f[color,#fb0300]\![*] \q[ON,cDEon] \f[color,default]\n \n[-100]\f[align,center]----\n \n[-100]\f[align,right] \![*] \q[OFF,cDEoff]\n[150]\f[align,left]"
	// }
	// else
	// {
		// "\![*] \q[ON,cDEon] \n \n[-100]\f[align,center]----\n \n[-100]\f[align,right] \f[color,#fb0300]\![*] \q[OFF,cDEoff]\f[color,default]\n[150]\f[align,left]"
	// }
	--
	"Enable Uninstall?\n"
	--
	if deleteshitai == "ON"
	{
		"\f[color,#fb0300]\![*] \q[ON,OnConfigSet,deleteon] \f[color,default]\n \n[-100]\f[align,center]----\n \n[-100]\f[align,right] \![*] \q[OFF,OnConfigSet,deleteoff]\n[150]\f[align,left]"
	}
	else
	{
		"\![*] \q[ON,OnConfigSet,deleteon] \n \n[-100]\f[align,center]----\n \n[-100]\f[align,right] \f[color,#fb0300]\![*] \q[OFF,OnConfigSet,deleteoff]\f[color,default]\n[150]\f[align,left]"
	}
	--
	"\![*] \q[%(userMenu),OnUserConfig]\n"
	--
	"\![*] \q[%(backMenu),OnMenuRun]\n"
	--
	"\![*] \q[%(closeMenu),CANCEL]\e"
}

OnConfigSet
{
	case reference[0]
	{
		when "deleteon" // Enable/Disable Uninstall
		{
			deleteshitai = "ON"
		}
		when "deleteoff"
		{
			deleteshitai = "OFF"
		}
		when "verBinOn" // Enable/Disable Verbose emptying recycle bin
		{
			verBalete = "ON"
		}
		when "verBinOff"
		{
			verBalete = "OFF"
		}
		when "silenton"	// Enable/Disable SilentMode
		{
			silentMode = "ON"
		}
		when "silentoff"
		{
			silentMode = "OFF"
		}
		// The following are preserved for when I get the feature working
		when "cDEon" // Enable/Disable DesktopEye
		{
			canDesktopEye = "ON"
		}
		when "cDEoff"
		{
			canDesktopEye = "OFF"
		}
	}
	--
	OnConfigMenu
}




// ---- Talk Rate ----
// This is when the user chooses to adjust the talk rate in the config menu. The talk rate is how often the ghost will read some dialogue from aitalk.dic.

OnSetTalk
{
	"\b2\0\s[3]"
	--
	"%(Flowey)* Change [talktime]?\_w[500]\0\s[3]\n talktime == %(talktime)."
	
	"\_v[mus\help_TEXT.mp3] You can change how often f_lowe_y speaks.\_w[500]\0\s[3] The current rate is every %(talktime)."
	--
	
	"\0\n[150]/
	\![*] \q[ Don't Talk,OnTalkSet,never]\n/
	\![*] \q[ Every minute,OnTalkSet,one]\n/
	\![*] \q[ Every 5 minutes,OnTalkSet,five]\n/
	\![*] \q[ Every 15 minutes,OnTalkSet,fifteen]\n/
	\![*] \q[ Every 30 minutes,OnTalkSet,thirty]\n"
	--
	'\![*] \q[ Custom Time,"script:\![open,sliderinput,OnTalkSet,,%(minutetalk),0,60]"]\n'
	--
	"\![*] \q[ Random Time,OnTalkSet,random]\n\n[half]/
	\![*] \q[ Don't change,OnTalkSet,stay]"
}


OnTalkSet
{
	"\0\s[3]%(Flowey) * "
	--
	case reference0
	{
		when "never"
		{
			minutetalk = 0
			talksetting = "never"
			aitalkinterval = 0
			
			"..."
		}
		when "one"
		{
			minutetalk = 1
			talksetting = "1 minute"
			aitalkinterval = 60
			
			"1 minute."
		}
		when "five"
		{
			minutetalk = 5
			talksetting = "5 minutes"
			aitalkinterval = 300
			
			"5 minutes."
		}
		when "fifteen"
		{
			minutetalk = 15
			talksetting = "15 minutes"
			aitalkinterval = 900
			
			"15 minutes."
		}
		when "thirty"
		{
			minutetalk = 30
			talksetting = "30 minutes"
			aitalkinterval = 1800
			
			"30 minutes."
		}
		when "random"
		{
			minutetalk = (RAND(60) + 1)
			talksetting = "%(minutetalk) minutes"
			aitalkinterval = (minutetalk * 60)
			
			"%(minutetalk) minutes."
		}
		when "stay"
		{
			"No change."
		}
		others
		{
			minutetalk = reference0
			if minutetalk != 0
			{
				talksetting = "%(minutetalk) minutes"
				aitalkinterval = (minutetalk * 60)
				
				"%(minutetalk) minutes."
			}
			else
			{
				talksetting = "never"
				aitalkinterval = 0
				
				"... \_w[500]\s[18]... \_w[500]\s[21]...\_w[500]"
			}
		}
	}
	--
	"\n[200]Talk... others?\n"
	--
	"\f[align,center]\q[No,OnCommuSet,0] ------ "
	--
	'\q[Yes,"script:%(Narrator) * What percentage of the time would you like %(callHim) to try to communicate with other ghosts? \![open,sliderinput,OnCommuSet,,%(communicateratio),0,100]"]'
}

OnCommuSet
{
	communicateratio = reference0
	--
	if communicateratio == 0
	{
		if ghostexcount > 0
		{"\![set,otherghosttalk,false]%(Narrator) * He'll never know he isn't alone."}
		else
		{"\![set,otherghosttalk,false]%(Narrator) * He'll never know how alone he is."}
		
	}
	else
	{
		"\![set,otherghosttalk,true]%(Narrator) * He will try to talk to other ghosts %(communicateratio)% of the time."
	}
}

	
// If the user selects cancel in a menu.
Select.CANCEL
{
	"\0\s[3]%(Flowey) * Nevermind. "
	
	"\0\s[3]%(Flowey) * Whatever. "
}

	
OnFunctionMenu
{

	"\0\s[3]%(Flowey)\b2 * ...\n[200]"
	"\0\s[3]%(Flowey)\b2 * ACC_SSING FUNCTION M_NU\n[200]"
	"\0\s[3]\_v[mus\help_TEXT.mp3]\b2 * (You ask f_lowe_y to do something for you.)\n[200]"
	
	
	--
	"\![*] \q[Open Calendar,CALENDAR]\n[150]/
	\![*] \q[Check Email,MailCheck]\n[150]/
	\![*] \q[Open SSP Preferences,sspconfig]\n[150]/
	\![*] \q[Empty Recycle Bin,OnRecycleStart]\n[150]/
	\![*] \q[System Information,OnRunCPUID]\n[150]\n/
	\![*] \q[View Ghost Changelog,OnChangeLog,menu]\n[150]\n/
	\![*] \q[%(backMenu),OnMenuRun]\n"
	--
	if username == "Era" || username == "BugTest"
	{
		"\n[200]\![*] \q[Test variable,OnTestVariable]\n"
	}
	else
	{
		"\e"
	}
}


OnStatMenu
{
	calcHP
	_wID = RAND(8) + 1
	_aID = RAND(8) + 1
	--
	"\b2\n  %(CHR(34))%(username)%(CHR(34)) "
	--
	"\n[300]"
	--
	"LV %(ellVee)\n/
	HP %(HP)/%(HP)\n[200]/
	AT NaN(NaN) EXP %(knifeTime)\n/
	DF NaN(NaN) Next %(toNextLV)\n[200]/
	WEAPON: weapon_id_%(_wID) \n/
	ARMOR: equip_id_%(_aID) \n[300]"
	--
	"\![*] \q[RESET STATS?,resetSTATS]\n[200]"
	--
	"\![*] \q[%(backMenu),OnMenuRun]\n"
	
	
}

toNextLV
{
	_killsLeft = 5-killCount
	_knifeleft = 20*_killsLeft
	_next = _knifeleft-knifeTime
	"%(_next)"
}

calcHP : void
{
	_hp = 20
	if ellVee <= 1 {HP = 20}
	elseif ellVee >= 20 {HP = 99}
	else
	{
		 for _i=1;_i<ellVee;_i++
		{
			_hp = _hp + 4
		}
		--
		HP = _hp;
	}
}

Select.resetSTATS
{
	EXP = 0
	HP = 20
	ellVee = 1
	knifeTime = 0
	killCount = 0
	resetCount++
	agreedToConsequences = IARRAY
	--
	OnStatMenu
}





//When the user clicks Open Calender. This will open the calender function of SSP, an in-built feature that you don't have to worry about in the least.
Select.CALENDAR
{
	"\0\s[3]%(Flowey)* Op_ning th_ cal_nder.\![open,calendar]"
}

//When the user chooses SSP preferences.
Select.sspconfig
{
	"\0\s[3]%(Flowey)* _pening the SSP c_nfig menu.\![open,configurationdialog]"
}
	
//This is when the user chooses to check their email. The rest of this function plays out in etc.dic.
Select.MailCheck
{
	"\![biff]\e"
}



//-------- Menu Caption Envelopes --------

// These are the envelopes containing all the multi-version menu options used

// This first one is ...something... because it's the whole line with tags and not just one phrase
menuIntro: pool
{
	{
		menuOption = 0
		if ellVee >=2
		{
			"\0\s[3]%(Flowey)* \![embed,OnGLITCH,oooooooooOOOOOOOOOoooooooooOOOOOOOOO]\n\![embed,OnGLITCH,open menu.]\_w[1500]\c\_q* Howdy!!"
			"\0\s[3]%(Flowey)* What-\![embed,OnGLITCH,what]-\![embed,OnGLITCH,what]- do you want?"
			"\0\s[3]%(Flowey)* \![embed,OnGLITCH,need]\![embed,OnGLITCH,need]NEED?"
		}
		else
		{
			"\0\s[3]%(Flowey)* oooooooooOOOOOOOOO\nOpe_ Me_u.\_w[1500]\c\_q* Howdy!!"
			"\0\s[3]%(Flowey)* What-\![embed,OnGLITCH,what]-do you want?"
			"\0\s[3]%(Flowey)* NEED?"
		}
	}
	{
		menuOption = 1
		if ellVee >= 2
		{
			"\0\s[3]\_v[mus\HELP_text.mp3]* (You tell him...)"
			"\0\s[3]\_v[mus\HELP_text.mp3]* (You tell f_lowe_y...)"
		}
		else
		{
			"\0\s[3]\_v[mus\HELP_text.mp3]* (You ask him to...)"
			"\0\s[3]\_v[mus\HELP_text.mp3]* (You ask f_lowe_y to...)"
		}
	}
}

//---- Conditional Choice captions ----

rlTalk: pool
{
	if ellVee <= 3
	{
		if menuOption == 0
		{
			"RE-RE-REPE_T"
		}
		else
		{
			"Say that again..."
			"Repeat Last Dialogue"
			"select.ONLASTTALK"
		}
		
	}
	elseif ellVee <= 7
	{
			if menuOption == 0
		{
			"AGAIN?"
		}
		else
		{
			"Repeat Last Dialogue"
			"Say. That. Again."
		}
	}
	
}

newTalk: pool
{
	"select.ONAITALK"
	"Say something."
	"Talk to me."
	"SA_ _NYTHING"
}

ghostChat
{
	"Chat with Others"
}
userMenu: pool
{
	"select.USERMENU"
	"User Menu"
	"User Settings"
	"Us_r _onf_gurat_on_"
}

configMenu: pool
{
	"select.CONFIGMENU"
	"Config Menu"
	"Configuration"
	"C_NFIGURATI_N"
}

funMenu: pool
{
	"select.ONFUNMENU"
	"Functions"
	"Tools"
	"FUNct_ons"
}

statsMenu: pool
{
	"select.ONSTATMENU"
	"Check Stats"
}

backMenu: pool
{
	"Backbackbackback."
	"Return to the menu"
}

closeMenu: pool
{
	if ellVee >=2
	{
		"Shut up."
		"Quit it."
		"Go away."
		"Nevermind."
		"Ugh."
	}
	else
	{
		"Nevermind"
		"N%(e_)V%(e_)RM%(i_)ND"
		"Close"
		"C%(l_)%(o_)%(s_)%(e_)"
		"Close Menu"
		"select.CANCEL"
	}
}

	
//---- OnChoiceTimeout ----
//If the user is given a menu with choices, but doesn't pick one of the choices, it will eventually time out.

OnChoiceTimeout
 {
	"\0\s[3]%(Flowey)* Come back when-when-when you actually want something."
 }
