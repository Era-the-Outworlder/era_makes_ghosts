//  ######                                                   
//  #     # #####   ####  #    #  ####  #    # #    #  ####  
//  #     # #    # #    # ##   # #    # #    # ##   # #      
//  ######  #    # #    # # #  # #    # #    # # #  #  ####  
//  #       #####  #    # #  # # #    # #    # #  # #      # 
//  #       #   #  #    # #   ## #    # #    # #   ## #    # 
//  #       #    #  ####  #    #  ####   ####  #    #  ####  

//-------- OnTeachPronouns --------
// This is called from either OnFirstBoot or the User Config menu to set/change the user's pronouns

OnTeachPronouns
{
	"\t\c\0\s[3]\b2%(Flowey) * Do. %(d_)o. %(d_)%(o_).... Pronouns?\_w[500]\n\n\n[half]\_q/
	\n\n\n/
	\![*] \q[He-His-Him,OnSetPronouns,1]\n/
	\![*] \q[She-Her-Her,OnSetPronouns,2]\n/	
	\![*] \q[They-Their-Them,OnSetPronouns,3]\n/
	\![*] \q[Something else...,OnTeachCustomPronoun]\0\s[0]"
	
	"\t\c\0\s[3]%(Narrator)\n\n\n * You are offered a choice of pronouns.\_w[500]\n\n\n[half]\_q/
	\![*] \q[He-His-Him,OnSetPronouns,1]\n/
	\![*] \q[She-Her-Her,OnSetPronouns,2]\n/	
	\![*] \q[They-Their-Them,OnSetPronouns,3]\n/
	\![*] \q[Something else...,OnTeachCustomPronoun]\0\s[0]"
}	

OnSetPronouns
{
	if reference0 == 1
	{
		nomPro = "he"
		oblPro = "him"
		posPro = "his"
	}
	elseif reference0 == 2
	{
		
		nomPro = "she"
		oblPro = "her"
		posPro = "her"
	}
	elseif reference0 == 3
	{
		
		nomPro = "they"
		oblPro = "them"
		posPro = "their"
	}
	elseif reference0 == 0
	{
		nomPro = reference1
		oblPro = reference2
		posPro = reference3
	}
	--
	
	if firstBoot == 1
	{
		"\0\s[3]%(Narrator) * You say that your pronouns are %(nomPro), %(oblPro), and %(posPro).\n\n f_lowe_y will now call you by the correct pronouns.\n\n You can always change this from f_low_ey's User Configuration Menu.\_w[500]\_s\b[-1]\c\_s"
		--
		"\![timerraise,1000,1,OnGetBirthday]"	
	}
	elseif firstBoot == 2
	{
		"\0\s[3]%(Narrator) * You say that your pronouns are %(nomPro), %(oblPro), and %(posPro).\n\n f_lowe_y will now call you by the correct pronouns.\n\n You can always change this from f_low_ey's User Configuration Menu.\_w[500]\_s\b[-1]\c\_s"
		--
		"\![timerraise,1000,1,OnGetBirthday]"	
	}
	else
	{
		"\0\s[3]%(Narrator) * Your pronouns are %(nomPro), %(oblPro), and %(posPro).\n\n f_lowe_y will now call you by the correct pronouns.\n\n You can always change this again later.\e"
	}

}


OnTeachCustomPronoun
{
	"\0\s[3]%(Narrator) Enter your nominative pronoun, ex.'they'\![open,inputbox,OnNominativeTeach,-1]"
}

//---- Teaching each pronoun ----

OnNominativeTeach
{
	_nounN = reference0 //this stores the pronoun value temporarily in reference0

	_check = REPLACE(reference0," ","")
	if _check  == "" // This is if the user doesn't enter any text
	{
		"\0\s[3]%(Narrator) You didn't type anything into the box. Whoops! \![open,inputbox,OnNominativeTeach,]"
	}
	else //Continuing on
	{
		"\0\s[3]%(Narrator) Enter your oblique pronoun, ex.'them'\![open,inputbox,OnObliqueTeach,--reference=%_nounN]"
	}
}


OnObliqueTeach
{
	_nounO = reference0 //this stores the pronoun value temporarily in reference0
	_nounN = reference2
	_check = REPLACE(reference0," ","")
	if _check  == "" // This is if the user doesn't enter any text
	{
		"\0\s[3]%(Narrator) You didn't type anything into the box. Whoops!\![open,inputbox,OnObliqueTeach,--reference=%_nounN]"
	}
	else //Continuing on
	{
		"\0\s[3]%(Narrator) Enter your possessive pronoun, ex.'their'\![open,inputbox,OnPossessiveTeach,--reference=%_nounN,--reference=%_nounO]"
	}
}


OnPossessiveTeach
{

	_nounP = reference0 //this stores the pronoun value temporarily in reference0
	_nounO = reference3
	_nounN = reference2
	_check = REPLACE(reference0," ","")
	if _check  == "" // This is if the user doesn't enter any text
	{
		"\0\s[3]%(Narrator) You didn't type anything into the box. Whoops!\![open,inputbox,OnPossessiveTeach,--reference=%_nounN,--reference=%_nounO]"
	}
	else //Continuing on
	{
		"\0\s[3]%(Narrator) * You entered %(_nounN), %(_nounO), and %(_nounP).\n * Does that seem right? \_w[500]\n\n[half]\_q/
		\![*]\q[Looks good!,OnSetPronouns,0,%_nounN,%_nounO,%_nounP]\n/
		\![*]\q[Let's try that again...,OnTeachCustomPronoun]"
			
	}
}
	



