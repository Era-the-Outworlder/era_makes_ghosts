/* ############# OnTranslate, Anchors, and Choices ############# */

// ----- OnTranslate -----

// ┌──────┬───────────────────────────────────────────┐
// │ Ref# │         Reference Contents                │
// ╞══════╪═══════════════════════════════════════════╡
// │ 0    │ The original script                       │
// │ 1    │ Source:                                   │
// │      │ communicate                               │
// │      │ sstp-send                                 │
// │      │ owned                                     │
// │      │ remote                                    │
// │      │ notranslate                               │
// │      │                                           │
// │      │ if sourced from a plugin,                 │
// │      │ reference1 will be followed by ",EventID" │
// │      │                                           │
// │ 2    │ EventID of the originating event          │
// │ 3    │ References of the originating event.      │
// └──────┴───────────────────────────────────────────┘

OnTranslate
{

	_talk = reference0
	
    if silentMode == "ON"
    {
        _talk = REPLACE(_talk, "\![sound,play,", "\![sound,stop,")
    }
    --
    if reference1 == "" && reference2 == "" // Script Input, script choice call
    {
        _talk = ('"' + REPLACE(_talk,'"','" + CHR(0x22) + "')+ '"')
		EVAL(_talk)
    }
	else
    { //event result from ghost
        _talk;
    }
}


// ----- OnChoiceSelect -----
// ┌───────┬──────────────────────────────────┐
// │ ref # │        Reference Contents        │
// ╞═══════╪══════════════════════════════════╡
// │ 0     │ ID of the selected choice.       │
// └───────┴──────────────────────────────────┘

OnChoiceSelect
{}

// ----- OnChoiceSelectEx, OnChoiceEnter, & OnChoiceHover -----
// ┌───────┬──────────────────────────────────┐
// │ ref # │        Reference Contents        │
// ╞═══════╪══════════════════════════════════╡
// │ 0     │ Choice text                      │
// │ 1     │ ID of the choice.                │
// │ 2+    │ Extended information             │
// └───────┴──────────────────────────────────┘

OnChoiceSelectEx
{
	"\p[2]*The papers shuffle at your choice*"
}

OnChoiceEnter
{
	"\C\p[2]*The papers shudder*"
}

OnChoiceHover
{
	"\C\p[2]\c%REFTEST"
}

//---- OnChoiceTimeout -----
// Called every time a choice is timed out, not just the main menu.
// ┌───────┬──────────────────────────────────┐
// │ ref # │        Reference Contents        │
// ╞═══════╪══════════════════════════════════╡
// │ 0     │ Raw text of timed-out choice     │
// └───────┴──────────────────────────────────┘

OnChoiceTimeout
{
	ERASEVAR("lastSource")
	--
	"This plays when a choice times out."
}


// ----- OnAnchorSelect -----
// ┌───────┬──────────────────────────────────┐
// │ ref # │        Reference Contents        │
// ╞═══════╪══════════════════════════════════╡
// │ 0     │ The ID of the selected anchor.   │
// └───────┴──────────────────────────────────┘

OnAnchorSelect
{}


// ----- OnAnchorSelectEx -----
// ┌───────┬──────────────────────────────────┐
// │ ref # │        Reference Contents        │
// ╞═══════╪══════════════════════════════════╡
// │ 0     │ The text of the selected anchor. │
// │ 1     │ The ID of the selected anchor.   │
// │ 2+    │ Extended information             │
// └───────┴──────────────────────────────────┘
OnAnchorSelectEx
{
	if "http://" _in_ reference0 || "https://" _in_ reference0  //checking to see if the link is a web address
	{
		if reference0 != reference1
		{
			"Link text and URL do not match. This may be an error."
		}
		else
		{
			"Opening %(reference0)... \j[%(reference0)]"
		}
	}
	elseif "http://" _in_ reference1 || "https://" _in_ reference1
	{
		"Opening %(reference0)... \j[%(reference1)]"
	}
	else //if it's NOT a web address
	{
		case reference1  // Checks against anchor ID
		{
			when "bugmail" //if the anchor ID is this value
			{
				"\j[mailto:bugreport@outworlderaffairs.com]\e"
			}
			when "anchor2" //if the link is this value
			{
				""
			}
			when "anchor3" //if the link is this value
			{
				""
			}
		}
	}
}

OnRecommendsiteChoice
{
	"https://plaintexttools.github.io/plain-text-table/"
}