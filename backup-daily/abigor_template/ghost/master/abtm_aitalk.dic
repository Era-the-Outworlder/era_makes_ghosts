OnAITalk
{
	if CHAIN.IDName
	{
		LOGGING("ChainTalk")
		ChainTalk
	}
	elseif TOSTR(offscreenflag) != ",,"
	{
		LOGGING("OnOffscreenTalk")
		OnOffscreenTalk
	}
	else
	{
		
		if RAND(100) < (communicateratio - 1)
		{
			LOGGING("OnStartCommunicate")
			OnStartCommunicate
		}
		else
		{
			LOGGING("RandomTalk")
			RandomTalk
		}
		
	}
}

RandomTalk : nonoverlap_pool
{

	"\0This is a line of test dialogue."
	"\1*The sabaton gives a bored clink*"
	"\p[2]*The papers rustle restlessly*"
	
	
	
	// ----- Timed Conditionals -----

		
	if ASEARCH('saidtimeslot',hourlyArray) == -1
	{OnTimeTalk}
	
	if ASEARCH('saidweekday',hourlyArray) == -1
	{OnDayTalk}
	
	if ASEARCH('saidmonthly',hourlyArray) == -1
	{OnMonthTalk}
	
	// ----- Chained Dialogue -----
	
	if ASEARCH('testchain',hourlyArray) == -1 
	{	
		"\0This is the first test chain. "
		--
		"\e:chain=testchain0 :eval=hourlyArray ,= 'testchain'"
	}
	
	if ASEARCH('forkchain',hourlyArray) == -1
	{
		"\0This Should be a forking chain. "
		--
		"\e:chain=forkchain0 :eval=hourlyArray ,= 'forkchain'"
	}
	
	if ASEARCH('choicechain',onetimeArray) == -1
	{
		"\0This is a chain that changes depending on how the user responds to a question. Yes or No?\n[200]"
		--
		"\![*] \q[Yes,ChainChoice,choicechainYES]\n[150]"
		--
		"\![*] \q[No,ChainChoice,choicechainNO]\n[150]"
	}
}


	
// ---- Chained Dialogue ----
testchain0
{{CHAIN
	"\0This is the second part of the first test chain. \e:chain=testchain1"
}}CHAIN

// ----- forkchain -----
forkchain0
{{CHAIN
	_test = RAND(100)
	"\0This is the second part of the FORKED test chain."
	--
	if _test <= 49
	{
		"\e:chain=forkchainA"
	}
	else
	{
		"\e:chain=forkchainB"
	}
}}CHAIN

forkchainA
{{CHAIN
	"\0\This is fork A of the forked test chain. \e:chain=end"
}}CHAIN

forkchainB
{{CHAIN
	"\0\This is fork B of the forked test chain. \e:chain=end"
}}CHAIN


// ----- choicechain -----
choicechainA
{{CHAIN
	"\0This is Chain that plays if you chose YES. \e:chain=choicechainA1"
}}CHAIN

choicechainA1
{{CHAIN
	"\0This ends the chain that plays if you chose YES. \e:chain=end"
}}CHAIN

choicechainB
{{CHAIN
	"\0This is Chain that plays if you chose NO. \e:chain=choicechainB1 :eval=onetimeArray ,= 'choicechain' "
}}CHAIN

choicechainB1
{{CHAIN
	"\0This ends the chain that plays if you chose NO. \e:chain=end :eval=onetimeArray ,= 'choicechain' "
}}CHAIN







OnTimeTalk
{
	"It's %(dishour):%(dismin)"
	--
	case hour
	{
		when 0-4 // weehours
		{
			" in the morning."
		}
		when 5-8 // earlymorning
		{
			" in the morning."
		}
		when 9-11 // morning
		{
			" in the morning."
		}
		when 12-14 // lunch
		{
			" in the afternoon."
		}
		when 15-17 // afternoon
		{
			" in the afternoon."
		}
		when 18-20 // evening
		{
			" in the evening."
		}
		when 21-23 // latenight
		{
			" at night."
		}
	}
	--
	if runhour == 1
	{runhour_s = ""}
	else
	{runhour_s = "s"}
	--
	if runmin == 1
	{runmin_s = ""}
	else
	{runmin_s = "s"}
	--
	"\nI have been running for \n%(runhour) hour%(runhour_s) and %(runmin) minute%(runmin_s)."
	
	--
	"\e:eval=hourlyArray ,= 'saidtimeslot'"
}

OnDayTalk
{
	"Today is "
	--
	if weekday == 0
	{
		"Sunday."
	}
	if weekday == 1
	{
		"Monday."
	}
	if weekday == 2
	{
		"Tuesday."
	}
	if weekday == 3
	{
		"Wednesday."
	}
	if weekday == 4
	{
		"Thursday."
	}
	if weekday == 5
	{
		"Friday."
	}
	if weekday == 6
	{
		"Saturday."
	}
	--
	"\e:eval=hourlyArray ,= 'saidweekday'"
}

OnMonthTalk
{
	"The current month is "
	--
	if month == 1
	{
		"January."
	}
	if month == 2
	{
		"February."
	}
	if month == 3
	{
		"March."
	}
	if month == 4
	{
		"April."
	}
	if month == 5
	{
		"May."
	}
	if month == 6
	{
		"June."
	}
	if month == 7
	{
		"July."
	}
	if month == 8
	{
		"August."
	}
	if month == 9
	{
		"September."
	}
	if month == 10
	{
		"October."
	}
	if month == 11
	{
		"November."
	}
	if month == 12
	{
		"December."
	}
	--
	"\e:eval=hourlyArray ,= 'saidmonthly'"
}

// ----- OnKeyPress -----

// ┌───────┬──────────────────────────────────────┐
// │ ref # │ Reference Contents                   │
// ╞═══════╪══════════════════════════════════════╡
// │ 0     │ Key entered (button pressed)         │
// │       │                                      │
// │ 1     │ Key entered (Win32 virtual key code) │
// │       │                                      │
// │ 2     │ Number of keypresses                 │
// │       │                                      │
// │ 3     │ Scope ID that sent the key event     │
// │       │                                      │
// │ 4     │ Modifier keys separated by commas    │
// │       │ e.g. "ctrl,alt"                      │
// └───────┴──────────────────────────────────────┘

OnKeyPress
{
	// "%(REFTEST('OnKeyPress'))"
	--
	case reference0
	{
		when "t"
		{
			OnAITalk
		}
		when "r"
		{
			SHIORI3FW.LastTalk 
		}
		when "h"
		{
			OnHelpMenu
		}
		when "f4"
		{
			OnClose
		}
		when "f1"
		{
			"\![open,readme]"
		}
	}
}


// ----- OnSecondChange, OnMinuteChange, & OnHourTimeSignal -----

// ┌───────┬────────────────────────────────────────────────┐
// │ ref # │ Reference Contents                             │
// ╞═══════╪════════════════════════════════════════════════╡
// │ 0     │ Continuous OS uptime in hours                  │
// │       │                                                │
// │ 1     │ Is sakura/scope1 offscreen:                    │
// │       │ "1" if yes                                     │
// │       │ "0" if no                                      │
// │       │                                                │
// │ 2     │ Is sakura/scope1 overlapping ker/scope2:       │
// │       │ "1" if yes                                     │
// │       │ "0" if no                                      │
// │       │                                                │
// │ 3     │ Can ghost talk now:                            │
// │       │ always "1" if OnHourTimeSignal, otherwise:     │
// │       │ "1" if yes                                     │
// │       │ "0" if no                                      │
// │       │                                                │
// │ 4     │ Seconds since last mouse movement- at OS level │
// └───────┴────────────────────────────────────────────────┘

OnSecondChange
{
	if runsec == 59
 	{
	 	runsec = 0
		if runmin == 59
		{
			runhour ++
			runmin = 0
			ERASEVAR("hourlyArray")
		}
		else
		{
			runmin ++
		}
	}
 	else
 	{
	 	runsec ++
	}
	--
}

OnMinuteChange
{
	if timeTillAsk != 0
	{
		timeTillAsk --
	}
}

OnHourTimeSignal
{
	case hour
	{
		when 0 //midnight
		{
			"It is midnight."
		}
		when 12
		{
			"It is noon."
		}
		when 1-11,13-23
		{
			"It is %(dishour) o'clock."
		}
	}
}